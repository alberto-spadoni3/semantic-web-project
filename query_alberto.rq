PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX schema: <https://schema.org/>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX anime: <http://www.semanticweb.org/ontology/anime/>


# Query 2
# Per ogni piattaforma di streaming, contare il numero di anime che sono disponibili con i sottotitoli in italiano.
SELECT ?platformTitle (COUNT(?anime) AS ?AnimeCount) WHERE {
    ?brd_event schema:publishedOn ?platform ;
               schema:subtitleLanguage anime:Language_it-IT ;
               anime:providesContentFor ?anime .
    ?platform dct:title ?platformTitle .
    # ?anime a dbpedia:Anime .
}
GROUP BY ?platform ?platformTitle


# Query 4
# A partire da una qualsiasi stringa, mostrare tutti gli anime che contengono
# tale stringa nel titolo e alcune informazioni relative.
SELECT 
	?anime_name ?animeAlterName ?animeURL 
    ?status ?producer
    ?rating ?audience
    (GROUP_CONCAT(?genre; SEPARATOR=", ") AS ?animeGenres)
WHERE {
  	?anime a dbpedia:Anime ;
           dct:title ?anime_name ;
           schema:url ?animeURL ;
           anime:creativeWorkStatus [dct:title ?status]	;
           schema:contentRating [ dct:title ?rating ] ;
           schema:audience [ schema:audienceType ?audience] ;
           schema:producer [ dct:title ?producer] ;
           schema:genre [ dct:title ?genre ] .

    OPTIONAL { 
        ?anime dct:alternative ?animeAlterName .
        FILTER(!sameTerm(?anime_name, ?animeAlterName)) .
    }

    FILTER regex(?anime_name, "${STRING}", "i") .
}
GROUP BY ?anime_name ?animeAlterName ?animeURL ?status ?producer ?rating ?audience


# Query 5
# I 5 voice actor con più personaggi doppiati
SELECT 
    ?voiceActorName (COUNT(?characterName) AS ?voicedAnimeCount)
    (GROUP_CONCAT(?characterName; SEPARATOR=", ") AS ?voicedCharactersName)
WHERE {
    ?voiceActor anime:voiceActed ?character ;
                schema:name ?voiceActorName .
    ?character schema:name ?characterName .
  MINUS { ?character schema:alternateName ?characterName}
}
GROUP BY ?voiceActorName
ORDER BY DESC(?voicedAnimeCount)
LIMIT 5


# Query 7
# Dato il nome di un character, fornire tutte le informazioni disponibili 
# (nome alternativo, descrizione, anime di appartenenza, lingue in cui è stato doppiato)
SELECT  
    ?character 
    ?characterAlterName 
    (SUBSTR(?description, 1, 20) AS ?characterDescription) # Riduce la lunghezza della descrizione
    (GROUP_CONCAT(DISTINCT ?animeName; SEPARATOR=" - ") AS ?presentInAnime)  
    (GROUP_CONCAT(DISTINCT ?voicingLang; SEPARATOR=", ") AS ?voicedInLanguages)  
WHERE {
  	?character schema:name "${NOME_PERSONAGGIO}" ;
               anime:animeWithCharacter ?anime ;
               anime:hasVoiceActor [ anime:languageOfVoicing ?languageOfVoicing ] .
               OPTIONAL { ?character schema:alternateName ?characterAlterName } .
	           OPTIONAL { ?character dct:description ?description } .
    ?anime dct:title ?animeName .
    MINUS {
        ?anime schema:alternateName ?animeName 
    }
	?languageOfVoicing schema:alternateName ?voicingLang .
    FILTER(LANG(?voicingLang) = "en").
}
GROUP BY ?character ?characterAlterName ?description

# Query 9
# Tutti gli anime presenti su Crunchyroll che sono disponibili in lingua italiana
SELECT ?anime_name ?streaming_url WHERE {
    ?brd_event schema:publishedOn anime:Broadcast_Crunchyroll ;
               schema:inLanguage anime:Language_it-IT ;
               anime:providesContentFor ?anime ;
               schema:url ?streaming_url .
    
    ?anime dct:title ?anime_name .
    MINUS { ?anime schema:alternateName ?anime_name }
}
